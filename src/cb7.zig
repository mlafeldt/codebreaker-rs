const std = @import("std");
const Rc4 = @import("rc4.zig").Rc4;

pub const Cb7 = struct {
    seeds: [5][256]u8,
    key: [5]u32,
    beefcodf: bool,
    initialized: bool,

    pub fn init() Cb7 {
        return .{
            .seeds = ZERO_SEEDS,
            .key = [_]u32{0} ** 5,
            .beefcodf = false,
            .initialized = false,
        };
    }

    pub fn initDefault() Cb7 {
        var cb = Cb7.init();
        cb.beefcode(BEEFCODE, 0);
        return cb;
    }

    pub fn beefcode(self: *Cb7, addr: u32, val: u32) void {
        std.debug.assert(isBeefcode(addr));

        var idx: [4]usize = undefined;
        const val_le = std.mem.nativeToLittle(u32, val);
        const bytes = @as([4]u8, @bitCast(val_le));
        for (bytes, 0..) |b, i| {
            idx[i] = b;
        }

        if (!self.initialized) {
            self.key = RC4_KEY;

            if (val != 0) {
                self.seeds = SEEDS;
                self.reseedKey(idx);
            } else {
                self.seeds = ZERO_SEEDS;
            }

            self.initialized = true;
        } else if (val != 0) {
            self.reseedKey(idx);
        } else {
            self.seeds = ZERO_SEEDS;
            for (self.key[0..4]) |*slot| {
                slot.* = 0;
            }
        }

        const key_bytes = std.mem.sliceAsBytes(self.key[0..]);
        for (self.seeds[0..]) |*seed| {
            var rc4 = Rc4.init(key_bytes);
            rc4.crypt(seed.*[0..]);
            rc4.crypt(key_bytes);
        }

        self.beefcodf = (addr & 1) != 0;
    }

    fn reseedKey(self: *Cb7, idx: [4]usize) void {
        for (0..4) |i| {
            self.key[i] =
                (@as(u32, self.seeds[(i + 3) % 4][idx[3]]) << 24) |
                (@as(u32, self.seeds[(i + 2) % 4][idx[2]]) << 16) |
                (@as(u32, self.seeds[(i + 1) % 4][idx[1]]) << 8) |
                @as(u32, self.seeds[i % 4][idx[0]]);
        }
    }

    pub fn encryptCode(self: *Cb7, addr: u32, val: u32) [2]u32 {
        var a = addr;
        var v = val;
        self.encryptCodeMut(&a, &v);
        return .{ a, v };
    }

    pub fn encryptCodeMut(self: *Cb7, addr: *u32, val: *u32) void {
        const oldaddr = addr.*;
        const oldval = val.*;

        addr.* = mulEncrypt(addr.*, self.key[0] -% self.key[1]);
        val.* = mulEncrypt(val.*, self.key[2] +% self.key[3]);

        var block = [2]u32{ addr.*, val.* };
        const key_bytes = std.mem.sliceAsBytes(self.key[0..]);
        var rc4 = Rc4.init(key_bytes);
        rc4.crypt(std.mem.sliceAsBytes(block[0..]));
        addr.* = block[0];
        val.* = block[1];

        rsaCrypt(addr, val, RSA_ENC_KEY, RSA_MODULUS);

        const seeds_bytes = std.mem.sliceAsBytes(self.seeds[0..]);
        const s = std.mem.bytesAsSlice(u32, seeds_bytes);
        for (0..64) |i| {
            addr.* = ((addr.* +% s[2 * 64 + i]) ^ s[i]) -% (val.* ^ s[4 * 64 + i]);
            val.* = ((val.* -% s[3 * 64 + i]) ^ s[64 + i]) +% (addr.* ^ s[4 * 64 + i]);
        }

        if (isBeefcode(oldaddr)) {
            self.beefcode(oldaddr, oldval);
            return;
        }

        if (self.beefcodf) {
            var beef_seed = [2]u32{ oldaddr, oldval };
            var beef_rc4 = Rc4.init(std.mem.sliceAsBytes(beef_seed[0..]));
            beef_rc4.crypt(std.mem.sliceAsBytes(self.seeds[0..]));
            self.beefcodf = false;
        }
    }

    pub fn decryptCode(self: *Cb7, addr: u32, val: u32) [2]u32 {
        var a = addr;
        var v = val;
        self.decryptCodeMut(&a, &v);
        return .{ a, v };
    }

    pub fn decryptCodeMut(self: *Cb7, addr: *u32, val: *u32) void {
        const seeds_bytes = std.mem.sliceAsBytes(self.seeds[0..]);
        const s = std.mem.bytesAsSlice(u32, seeds_bytes);
        var idx: usize = 64;
        while (idx > 0) {
            idx -= 1;
            val.* = ((val.* -% (addr.* ^ s[4 * 64 + idx])) ^ s[64 + idx]) +% s[3 * 64 + idx];
            addr.* = ((addr.* +% (val.* ^ s[4 * 64 + idx])) ^ s[idx]) -% s[2 * 64 + idx];
        }

        rsaCrypt(addr, val, RSA_DEC_KEY, RSA_MODULUS);

        var block = [2]u32{ addr.*, val.* };
        const key_bytes = std.mem.sliceAsBytes(self.key[0..]);
        var rc4 = Rc4.init(key_bytes);
        rc4.crypt(std.mem.sliceAsBytes(block[0..]));
        addr.* = block[0];
        val.* = block[1];

        addr.* = mulDecrypt(addr.*, self.key[0] -% self.key[1]);
        val.* = mulDecrypt(val.*, self.key[2] +% self.key[3]);

        if (self.beefcodf) {
            var beef_seed = [2]u32{ addr.*, val.* };
            var beef_rc4 = Rc4.init(std.mem.sliceAsBytes(beef_seed[0..]));
            beef_rc4.crypt(std.mem.sliceAsBytes(self.seeds[0..]));
            self.beefcodf = false;
            return;
        }

        if (isBeefcode(addr.*)) {
            self.beefcode(addr.*, val.*);
        }
    }
};

pub fn isBeefcode(addr: u32) bool {
    return addr & 0xffff_fffe == BEEFCODE;
}

fn mulEncrypt(a: u32, b: u32) u32 {
    return a *% (b | 1);
}

fn mulDecrypt(a: u32, b: u32) u32 {
    return a *% modInverse(b | 1);
}

fn modInverse(x: u32) u32 {
    var y = x;
    inline for (0..4) |_| {
        y = y *% (2 -% y *% x);
    }
    return y;
}

fn rsaCrypt(addr: *u32, val: *u32, rsakey: u64, modulus: u64) void {
    const code = (@as(u64, addr.*) << 32) | @as(u64, val.*);
    if (code >= modulus) return;
    const result = modExp(code, rsakey, modulus);
    addr.* = @as(u32, @intCast(result >> 32));
    val.* = @as(u32, @intCast(result & 0xffff_ffff));
}

fn modExp(base: u64, exp: u64, modulus: u64) u64 {
    var result: u64 = 1 % modulus;
    var b = base % modulus;
    var e = exp;

    while (e != 0) {
        if ((e & 1) == 1) {
            result = mulMod(result, b, modulus);
        }
        e >>= 1;
        if (e != 0) {
            b = mulMod(b, b, modulus);
        }
    }

    return result;
}

fn mulMod(a: u64, b: u64, modulus: u64) u64 {
    const prod = @as(u128, a) * @as(u128, b);
    return @as(u64, @intCast(prod % @as(u128, modulus)));
}

const BEEFCODE: u32 = 0xbeef_c0de;

const RC4_KEY = [_]u32{
    0xd0db_a9d7,
    0x13a0_a96c,
    0x8041_0df0,
    0x2ccd_be1f,
    0xe570_a86b,
};

const RSA_DEC_KEY: u64 = 11;
const RSA_ENC_KEY: u64 = 2_682_110_966_135_737_091;
const RSA_MODULUS: u64 = 18_446_744_073_709_551_605;

const ZERO_SEEDS = [_][256]u8{
    [_]u8{0} ** 256,
    [_]u8{0} ** 256,
    [_]u8{0} ** 256,
    [_]u8{0} ** 256,
    [_]u8{0} ** 256,
};

const SEEDS = [_][256]u8{
    [_]u8{
        0x84, 0x01, 0x21, 0xa4, 0xfa, 0x4d, 0x50, 0x8d, 0x75, 0x33, 0xc5, 0xf7, 0x4a, 0x6d, 0x7c, 0xa6,
        0x1c, 0xf8, 0x40, 0x18, 0xa1, 0xb3, 0xa2, 0xf9, 0x6a, 0x19, 0x63, 0x66, 0x29, 0xae, 0x10, 0x75,
        0x84, 0x7d, 0xec, 0x6a, 0xf9, 0x2d, 0x8e, 0x33, 0x44, 0x5c, 0x33, 0x6d, 0x78, 0x3e, 0x1b, 0x6c,
        0x02, 0xe0, 0x7d, 0x77, 0x1d, 0xb1, 0x61, 0x2a, 0xcd, 0xc1, 0x38, 0x53, 0x1f, 0xa1, 0x6e, 0x3d,
        0x03, 0x0d, 0x05, 0xdc, 0x50, 0x19, 0x85, 0x89, 0x9b, 0xf1, 0x8a, 0xc2, 0xd1, 0x5c, 0x22, 0xc4,
        0x11, 0x29, 0xf6, 0x13, 0xec, 0x06, 0xe4, 0xbd, 0x08, 0x9e, 0xb7, 0x8d, 0x72, 0x92, 0x10, 0x3c,
        0x41, 0x4e, 0x81, 0x55, 0x08, 0x9c, 0xa3, 0xbc, 0xa1, 0x79, 0xb0, 0x7a, 0x94, 0x3a, 0x39, 0x95,
        0x7a, 0xc6, 0x96, 0x21, 0xb0, 0x07, 0x17, 0x5e, 0x53, 0x54, 0x08, 0xcf, 0x85, 0x6c, 0x4b, 0xbe,
        0x30, 0x82, 0xdd, 0x1d, 0x3a, 0x24, 0x3c, 0xb2, 0x67, 0x0c, 0x36, 0x03, 0x51, 0x60, 0x3f, 0x67,
        0xf1, 0xb2, 0x77, 0xdc, 0x12, 0x9d, 0x7b, 0xce, 0x65, 0xf8, 0x75, 0xea, 0x23, 0x63, 0x99, 0x54,
        0x37, 0xc0, 0x3c, 0x42, 0x77, 0x12, 0xb7, 0xca, 0x54, 0xf1, 0x26, 0x1d, 0x1e, 0xd1, 0xab, 0x2c,
        0xaf, 0xb6, 0x91, 0x2e, 0xbd, 0x84, 0x0b, 0xf2, 0x1a, 0x1e, 0x26, 0x1e, 0x00, 0x12, 0xb7, 0x77,
        0xd6, 0x61, 0x1c, 0xce, 0xa9, 0x10, 0x19, 0xaa, 0x88, 0xe6, 0x35, 0x29, 0x32, 0x5f, 0x57, 0xa7,
        0x94, 0x93, 0xa1, 0x2b, 0xeb, 0x9b, 0x17, 0x2a, 0xaa, 0x60, 0xd5, 0x19, 0xb2, 0x4e, 0x5a, 0xe2,
        0xc9, 0x4a, 0x00, 0x68, 0x6e, 0x59, 0x36, 0xa6, 0xa0, 0xf9, 0x19, 0xa2, 0xc7, 0xc9, 0xd4, 0x29,
        0x5c, 0x99, 0x3c, 0x5c, 0xe2, 0xcb, 0x94, 0x40, 0x8b, 0xf4, 0x3b, 0xd2, 0x38, 0x7d, 0xbf, 0xd0,
    },
    [_]u8{
        0xcc, 0x6d, 0x5d, 0x0b, 0x70, 0x25, 0x5d, 0x68, 0xfe, 0xbe, 0x6c, 0x3f, 0xa4, 0xd9, 0x95, 0x5f,
        0x30, 0xae, 0x34, 0x39, 0x00, 0x89, 0xdc, 0x5a, 0xc8, 0x82, 0x24, 0x3a, 0xfc, 0xda, 0x3c, 0x1f,
        0x73, 0x3f, 0x63, 0xaa, 0x53, 0xbd, 0x4e, 0xb5, 0x33, 0x48, 0x59, 0xc1, 0xb7, 0xe0, 0x0c, 0x99,
        0xec, 0x3b, 0x32, 0x26, 0xb3, 0xb1, 0xe2, 0x8e, 0x54, 0x41, 0x55, 0xdb, 0x1d, 0x90, 0x0b, 0x48,
        0xf3, 0x3f, 0xca, 0x1f, 0x19, 0xeb, 0x7f, 0x56, 0x52, 0xd7, 0x20, 0x67, 0x59, 0x4f, 0x4e, 0xdc,
        0xbb, 0x6a, 0x8e, 0x45, 0x88, 0x0b, 0x93, 0xac, 0xcd, 0x0e, 0x29, 0x18, 0x7a, 0x16, 0x8d, 0x8d,
        0xc2, 0x88, 0x6a, 0x9d, 0x39, 0xf4, 0x93, 0x14, 0xcd, 0xe0, 0x6b, 0xc7, 0x28, 0x21, 0x5c, 0x97,
        0x70, 0x7c, 0xab, 0x53, 0x46, 0x33, 0x03, 0x18, 0xdf, 0x91, 0xfe, 0x06, 0xc0, 0xff, 0xa2, 0x58,
        0xf3, 0xb0, 0x6b, 0x9b, 0x71, 0x91, 0x23, 0xda, 0x92, 0x67, 0x14, 0x34, 0x9f, 0xa5, 0xaf, 0x65,
        0x62, 0xe8, 0x7f, 0x79, 0x35, 0x32, 0x29, 0x3e, 0x4f, 0xdc, 0xc7, 0x8e, 0xf1, 0x21, 0x9d, 0x3b,
        0x61, 0xfc, 0x0b, 0x02, 0xec, 0xe4, 0xa7, 0xea, 0x77, 0xe7, 0x21, 0x63, 0x97, 0x7f, 0x23, 0x8a,
        0x8b, 0xbe, 0x4e, 0x90, 0xc0, 0x89, 0x04, 0x44, 0x90, 0x57, 0x41, 0xb5, 0x74, 0xad, 0xb1, 0xe9,
        0xf3, 0x91, 0xc7, 0x27, 0x3e, 0x00, 0x81, 0x99, 0xee, 0x38, 0xf5, 0x32, 0x4f, 0x27, 0x4f, 0x64,
        0x39, 0x3d, 0xd3, 0x0b, 0x99, 0xd5, 0x99, 0xd6, 0x10, 0x4b, 0x43, 0x17, 0x38, 0x34, 0x54, 0x63,
        0x19, 0x36, 0xbd, 0x15, 0xb1, 0x06, 0x1e, 0xde, 0x1b, 0xaf, 0xeb, 0xfa, 0x56, 0xb8, 0x8d, 0x9d,
        0x14, 0x1a, 0xa6, 0x49, 0x56, 0x19, 0xca, 0xc1, 0x40, 0x6d, 0x71, 0xde, 0x68, 0xc1, 0xc3, 0x4a,
    },
    [_]u8{
        0x69, 0x31, 0x5c, 0xab, 0x7f, 0x5b, 0xe9, 0x81, 0x32, 0x58, 0x32, 0x0a, 0x97, 0xf3, 0xc7, 0xcf,
        0xbb, 0x1d, 0xcf, 0x0e, 0x83, 0x35, 0x4c, 0x58, 0xce, 0xf7, 0x8a, 0xe4, 0xb0, 0xe4, 0x83, 0x48,
        0x81, 0x77, 0x7c, 0x3f, 0xbc, 0x27, 0x3a, 0x1b, 0xa4, 0xe9, 0x06, 0xa4, 0x15, 0xab, 0x90, 0x10,
        0x7d, 0x74, 0xda, 0xfc, 0x36, 0x09, 0xcc, 0xf7, 0x12, 0xb6, 0xf4, 0x94, 0xe9, 0x8b, 0x6a, 0x3b,
        0x5e, 0x71, 0x46, 0x3e, 0x0b, 0x78, 0xad, 0x3b, 0x94, 0x5b, 0x89, 0x85, 0xa3, 0xe0, 0x01, 0xeb,
        0x84, 0x41, 0xaa, 0xd7, 0xb3, 0x17, 0x16, 0xc3, 0x6c, 0xb1, 0x81, 0x73, 0xec, 0xe4, 0x6e, 0x09,
        0x56, 0xee, 0x7a, 0xf6, 0x75, 0x6a, 0x73, 0x95, 0x8d, 0xda, 0x51, 0x63, 0x8b, 0xbb, 0xe0, 0x4d,
        0xf8, 0xa0, 0x27, 0xf2, 0x9f, 0xc8, 0x15, 0x5a, 0x23, 0x85, 0x58, 0x04, 0x4a, 0x57, 0x28, 0x20,
        0x6d, 0x9d, 0x85, 0x83, 0x3c, 0xbf, 0x02, 0xb0, 0x96, 0xe8, 0x73, 0x6f, 0x20, 0x6e, 0xb0, 0xe4,
        0xc6, 0xfa, 0x71, 0xa6, 0x5d, 0xc5, 0xa0, 0xa3, 0xf8, 0x5c, 0x99, 0xcb, 0x9c, 0x04, 0x3a, 0xb2,
        0x04, 0x8d, 0xa2, 0x9d, 0x32, 0xf0, 0xbd, 0xaa, 0xea, 0x81, 0x79, 0xe2, 0xa1, 0xba, 0x89, 0x12,
        0xd5, 0x9f, 0x81, 0xeb, 0x63, 0xe7, 0xe5, 0xd4, 0xe9, 0x0e, 0x30, 0xbc, 0xcb, 0x70, 0xdd, 0x51,
        0x77, 0xc0, 0x80, 0xb3, 0x49, 0x03, 0x9a, 0xb8, 0x8c, 0xa7, 0x63, 0x62, 0x8f, 0x72, 0x5c, 0xa6,
        0xa0, 0xcf, 0x4f, 0xb4, 0x86, 0xfd, 0x49, 0xfa, 0x4a, 0x85, 0xdb, 0xfe, 0x61, 0xb7, 0x3a, 0xd7,
        0x83, 0x70, 0x57, 0x49, 0x83, 0xa7, 0x10, 0x73, 0x74, 0x37, 0x87, 0xfd, 0x6b, 0x28, 0xb7, 0x31,
        0x1e, 0x54, 0x1c, 0xe9, 0xd0, 0xb1, 0xca, 0x76, 0x3b, 0x21, 0xf7, 0x67, 0xbb, 0x48, 0x69, 0x39,
    },
    [_]u8{
        0x8d, 0xd1, 0x8c, 0x7b, 0x83, 0x8c, 0xa8, 0x18, 0xa7, 0x4a, 0x14, 0x03, 0x88, 0xb3, 0xce, 0x74,
        0xbf, 0x5b, 0x87, 0x67, 0xa7, 0x85, 0x6b, 0x62, 0x96, 0x7c, 0xa9, 0xa6, 0xf6, 0x9e, 0xf4, 0x73,
        0xc5, 0xc4, 0xb0, 0x2b, 0x73, 0x2e, 0x36, 0x77, 0xdf, 0xba, 0x57, 0xff, 0x7f, 0xe9, 0x84, 0xe1,
        0x8d, 0x7b, 0xa2, 0xef, 0x4f, 0x10, 0xf3, 0xd3, 0xe8, 0xb4, 0xba, 0x20, 0x28, 0x79, 0x18, 0xd6,
        0x0f, 0x1c, 0xaa, 0xbd, 0x0e, 0x45, 0xf7, 0x6c, 0x68, 0xb9, 0x29, 0x40, 0x1a, 0xcf, 0xb6, 0x0a,
        0x13, 0xf8, 0xc0, 0x9c, 0x87, 0x10, 0x36, 0x14, 0x73, 0xa1, 0x75, 0x27, 0x14, 0x55, 0xaf, 0x78,
        0x9a, 0x08, 0xc9, 0x05, 0xf2, 0xec, 0x24, 0x1b, 0x07, 0x4a, 0xdc, 0xf6, 0x48, 0xc6, 0x25, 0xcd,
        0x12, 0x1d, 0xaf, 0x51, 0x8f, 0xe9, 0xca, 0x2c, 0x80, 0x57, 0x78, 0xb7, 0x96, 0x07, 0x19, 0x77,
        0x6e, 0x16, 0x45, 0x47, 0x8e, 0x9c, 0x18, 0x55, 0xf1, 0x72, 0xb3, 0x8a, 0xea, 0x4e, 0x8d, 0x90,
        0x2e, 0xbc, 0x08, 0xac, 0xf6, 0xa0, 0x5c, 0x16, 0xe3, 0x7a, 0xee, 0x67, 0xb8, 0x58, 0xdc, 0x16,
        0x40, 0xed, 0xf9, 0x18, 0xb3, 0x0e, 0xd8, 0xee, 0xe1, 0xfa, 0xc3, 0x9f, 0x82, 0x99, 0x32, 0x41,
        0x34, 0xbe, 0xc9, 0x50, 0x36, 0xe5, 0x66, 0xaa, 0x0d, 0x43, 0xf0, 0x3f, 0x26, 0x7c, 0xf3, 0x87,
        0x26, 0xa4, 0xf5, 0xf8, 0xa0, 0x32, 0x46, 0x74, 0x2e, 0x5a, 0xe2, 0xe7, 0x6b, 0x02, 0xa8, 0xd0,
        0xcf, 0xb8, 0x33, 0x15, 0x3b, 0x4f, 0xc7, 0x7a, 0xe8, 0x3d, 0x75, 0xd2, 0xfe, 0x42, 0x22, 0x22,
        0xa8, 0x21, 0x33, 0xfb, 0xb0, 0x87, 0x92, 0x99, 0xca, 0xd7, 0xd7, 0x88, 0xac, 0xe4, 0x75, 0x83,
        0x56, 0xbf, 0xce, 0xed, 0x4f, 0xf6, 0x22, 0x07, 0xca, 0xbc, 0xd2, 0xef, 0x1b, 0x75, 0xd6, 0x2d,
    },
    [_]u8{
        0xd2, 0x4f, 0x76, 0x51, 0xeb, 0xa1, 0xad, 0x84, 0xd6, 0x19, 0xe6, 0x97, 0xd9, 0xd3, 0x58, 0x6b,
        0xfb, 0xb8, 0x20, 0xfd, 0x49, 0x56, 0x1b, 0x50, 0x61, 0x10, 0x57, 0xb8, 0x78, 0x07, 0xc1, 0x4a,
        0xa2, 0xea, 0x47, 0x80, 0x00, 0x4a, 0xb3, 0x4e, 0x6f, 0x1a, 0xc1, 0xd5, 0x22, 0xf8, 0x54, 0x2f,
        0x33, 0xe5, 0x7f, 0xb4, 0x13, 0x02, 0xa3, 0xa1, 0x8b, 0x1c, 0x6f, 0x19, 0xd6, 0x42, 0xb3, 0x24,
        0x4b, 0x04, 0x30, 0x10, 0x02, 0x23, 0x6f, 0x10, 0x03, 0x4b, 0x0e, 0x33, 0x55, 0x22, 0xa4, 0x78,
        0xec, 0xd2, 0x4a, 0x11, 0x8b, 0xfc, 0xff, 0x14, 0x7a, 0xed, 0x06, 0x47, 0x86, 0xfc, 0xf0, 0x03,
        0x0f, 0x75, 0x07, 0xe4, 0x9a, 0xd3, 0xbb, 0x0d, 0x97, 0x1f, 0x6f, 0x80, 0x62, 0xa6, 0x9e, 0xc6,
        0xb1, 0x10, 0x81, 0xa1, 0x6d, 0x55, 0x0f, 0x9e, 0x1b, 0xb7, 0xf5, 0xdc, 0x62, 0xa8, 0x63, 0x58,
        0xcf, 0x2f, 0x6a, 0xad, 0x5e, 0xd3, 0x3f, 0xbd, 0x8d, 0x9b, 0x2a, 0x8b, 0xdf, 0x60, 0xb9, 0xaf,
        0xaa, 0x70, 0xb4, 0xa8, 0x17, 0x99, 0x72, 0xb9, 0x88, 0x9d, 0x3d, 0x2a, 0x11, 0x87, 0x1e, 0xf3,
        0x9d, 0x33, 0x8d, 0xed, 0x52, 0x60, 0x36, 0x71, 0xff, 0x7b, 0x37, 0x84, 0x3d, 0x27, 0x9e, 0xd9,
        0xdf, 0x58, 0xf7, 0xc2, 0x58, 0x0c, 0x9d, 0x5e, 0xee, 0x23, 0x83, 0x70, 0x3f, 0x95, 0xbc, 0xf5,
        0x42, 0x86, 0x91, 0x5b, 0x3f, 0x77, 0x31, 0xd2, 0xb7, 0x09, 0x59, 0x53, 0xf5, 0xf2, 0xe5, 0xf1,
        0xdc, 0x92, 0x83, 0x14, 0xc1, 0xa2, 0x25, 0x62, 0x13, 0xfd, 0xd4, 0xc5, 0x54, 0x9d, 0x9c, 0x27,
        0x6c, 0xc2, 0x75, 0x8b, 0xbc, 0xc7, 0x4e, 0x0a, 0xf6, 0x5c, 0x2f, 0x12, 0x8e, 0x25, 0xbb, 0xf2,
        0x5f, 0x89, 0xaa, 0xea, 0xd9, 0xcd, 0x05, 0x74, 0x20, 0xd6, 0x17, 0xed, 0xf0, 0x66, 0x6c, 0x7b,
    },
};

const mul_tests = [_]struct { enc: u32, key: u32, plain: u32 }{
    .{ .enc = 0x0000_0000, .key = 0xa686_d3b6, .plain = 0x0000_0000 },
    .{ .enc = 0x000e_0000, .key = 0xa686_d3b6, .plain = 0xac62_0000 },
    .{ .enc = 0x0067_bd20, .key = 0x4fd9_31ff, .plain = 0x2008_02e0 },
    .{ .enc = 0x2ba0_a76e, .key = 0xa686_d3b6, .plain = 0x2405_0002 },
    .{ .enc = 0x4adf_d954, .key = 0x4fd9_31ff, .plain = 0x9029_beac },
    .{ .enc = 0x7c01_6806, .key = 0x2912_dedd, .plain = 0x0000_00be },
    .{ .enc = 0xa942_2f21, .key = 0xa686_d3b6, .plain = 0x03d2_03e7 },
    .{ .enc = 0xfff5_76e0, .key = 0xa686_d3b6, .plain = 0x27bd_0020 },
};

const mod_inverse_tests = [_]struct { x: u32, inv: u32 }{
    .{ .x = 0x0d31_3243, .inv = 0x6c7b_2a6b },
    .{ .x = 0x0efd_8231, .inv = 0xd4c0_96d1 },
    .{ .x = 0x2912_dedd, .inv = 0xe09d_e975 },
    .{ .x = 0x4fd9_31ff, .inv = 0x9a62_cdff },
    .{ .x = 0x5a53_abb5, .inv = 0x58f4_2a9d },
    .{ .x = 0x9ab2_af6d, .inv = 0x1043_b265 },
    .{ .x = 0xa686_d3b7, .inv = 0x57ed_7a07 },
    .{ .x = 0xec35_a92f, .inv = 0xd274_3dcf },
    .{ .x = 0x0000_0000, .inv = 0x0000_0000 },
    .{ .x = 0x0000_0001, .inv = 0x0000_0001 },
    .{ .x = 0xffff_ffff, .inv = 0xffff_ffff },
};

const cb_tests = [_]struct {
    beefcode: [2]u32,
    decrypted: []const [2]u32,
    encrypted: []const [2]u32,
}{
    .{
        .beefcode = .{ 0xbeef_c0de, 0x0000_0000 },
        .decrypted = &[_][2]u32{
            .{ 0x9029_beac, 0x0c0a_9225 },
            .{ 0x201f_6024, 0x0000_0000 },
            .{ 0x2096_f5b8, 0x0000_00be },
        },
        .encrypted = &[_][2]u32{
            .{ 0xd08f_3a49, 0x0007_8a53 },
            .{ 0x3818_dde5, 0xe72b_2b16 },
            .{ 0x973e_0b2a, 0xa7d4_af10 },
        },
    },
    .{
        .beefcode = .{ 0xbeef_c0de, 0xdead_face },
        .decrypted = &[_][2]u32{
            .{ 0x9029_beac, 0x0c0a_9225 },
            .{ 0x201f_6024, 0x0000_0000 },
            .{ 0x2096_f5b8, 0x0000_00be },
        },
        .encrypted = &[_][2]u32{
            .{ 0xe65b_5422, 0xb125_43cf },
            .{ 0xd14f_5e52, 0xfe26_c9ed },
            .{ 0xdd9b_b6f0, 0xf5df_87f7 },
        },
    },
    .{
        .beefcode = .{ 0xbeef_c0df, 0xb16b_00b5 },
        .decrypted = &[_][2]u32{
            .{ 0x0123_4567, 0x89ab_cdef },
            .{ 0x9029_beac, 0x0c0a_9225 },
            .{ 0x201f_6024, 0x0000_0000 },
            .{ 0x2096_f5b8, 0x0000_00be },
        },
        .encrypted = &[_][2]u32{
            .{ 0x8623_16ab, 0xc59c_5fb1 },
            .{ 0x0613_3b66, 0x9544_4ff1 },
            .{ 0x565f_d08d, 0x9154_aff4 },
            .{ 0x4ef4_12fe, 0xd03e_4e13 },
        },
    },
    .{
        .beefcode = .{ 0xbeef_c0de, 0x0000_0000 },
        .decrypted = &[_][2]u32{
            .{ 0xbeef_c0df, 0xb16b_00b5 },
            .{ 0x0123_4567, 0x89ab_cdef },
            .{ 0x9029_beac, 0x0c0a_9225 },
            .{ 0x201f_6024, 0x0000_0000 },
            .{ 0x2096_f5b8, 0x0000_00be },
        },
        .encrypted = &[_][2]u32{
            .{ 0xfe8b_8601, 0xc7c6_f6ce },
            .{ 0x2195_d855, 0x63fa_11a7 },
            .{ 0x0ca3_1760, 0xa6f7_e88a },
            .{ 0x679d_c392, 0xfa43_e30b },
            .{ 0x1cd9_ccc3, 0x6af7_4e36 },
        },
    },
    .{
        .beefcode = .{ 0xbeef_c0de, 0x0000_0000 },
        .decrypted = &[_][2]u32{
            .{ 0xbeef_c0de, 0x0000_0000 },
            .{ 0x9029_beac, 0x0c0a_9225 },
            .{ 0x201f_6024, 0x0000_0000 },
            .{ 0x2096_f5b8, 0x0000_00be },
        },
        .encrypted = &[_][2]u32{
            .{ 0x8787_c575, 0x1ac4_c1b4 },
            .{ 0x0221_0430, 0x184c_16e8 },
            .{ 0x32e2_a916, 0x7e60_17ba },
            .{ 0xcbb7_20fd, 0xd615_05e0 },
        },
    },
};

test "cb7 mul helpers" {
    for (mul_tests) |t| {
        try std.testing.expectEqual(t.enc, mulEncrypt(t.plain, t.key));
        try std.testing.expectEqual(t.plain, mulDecrypt(t.enc, t.key));
    }
}

test "cb7 mod inverse" {
    for (mod_inverse_tests) |t| {
        try std.testing.expectEqual(t.inv, modInverse(t.x));
    }
}

test "cb7 encrypt_code" {
    for (cb_tests) |case| {
        var cb = Cb7.init();
        cb.beefcode(case.beefcode[0], case.beefcode[1]);

        for (case.decrypted, 0..) |code, idx| {
            const result = cb.encryptCode(code[0], code[1]);
            try std.testing.expectEqual(case.encrypted[idx], result);
            if (isBeefcode(code[0])) {
                cb.beefcode(code[0], code[1]);
            }
        }
    }
}

test "cb7 encrypt_code_mut" {
    for (cb_tests) |case| {
        var cb = Cb7.init();
        cb.beefcode(case.beefcode[0], case.beefcode[1]);

        var buf: [8][2]u32 = undefined;
        var len: usize = 0;
        for (case.decrypted) |code| {
            buf[len] = code;
            len += 1;
        }

        for (buf[0..len], 0..) |*code, idx| {
            const raw = code.*;
            cb.encryptCodeMut(&code.*[0], &code.*[1]);
            try std.testing.expectEqual(case.encrypted[idx], code.*);
            if (isBeefcode(raw[0])) {
                cb.beefcode(raw[0], raw[1]);
            }
        }
    }
}

test "cb7 decrypt_code" {
    for (cb_tests) |case| {
        var cb = Cb7.init();
        cb.beefcode(case.beefcode[0], case.beefcode[1]);

        for (case.encrypted, 0..) |code, idx| {
            const result = cb.decryptCode(code[0], code[1]);
            try std.testing.expectEqual(case.decrypted[idx], result);
            if (isBeefcode(result[0])) {
                cb.beefcode(result[0], result[1]);
            }
        }
    }
}

test "cb7 decrypt_code_mut" {
    for (cb_tests) |case| {
        var cb = Cb7.init();
        cb.beefcode(case.beefcode[0], case.beefcode[1]);

        var buf: [8][2]u32 = undefined;
        var len: usize = 0;
        for (case.encrypted) |code| {
            buf[len] = code;
            len += 1;
        }

        for (buf[0..len], 0..) |*code, idx| {
            cb.decryptCodeMut(&code.*[0], &code.*[1]);
            try std.testing.expectEqual(case.decrypted[idx], code.*);
            if (isBeefcode(code.*[0])) {
                cb.beefcode(code.*[0], code.*[1]);
            }
        }
    }
}
